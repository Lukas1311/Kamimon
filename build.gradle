plugins {
    id 'java'
    id 'jacoco'

    // https://plugins.gradle.org/plugin/org.openjfx.javafxplugin
    id 'org.openjfx.javafxplugin' version '0.0.13'

    id 'com.github.johnrengelman.shadow' version '8.1.1'


}

group 'de.uniks'
version '4.0.0'

repositories {
    mavenCentral()
}

shadowJar {
    mergeServiceFiles()
}

jar {
    manifest {
        attributes("Main-Class": "de.uniks.stpmon.k.Main")
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


dependencies {

    // -------------------- Styling ------------------------------------
    // https://mvnrepository.com/artifact/fr.brouillard.oss/cssfx
    implementation group: 'fr.brouillard.oss', name: 'cssfx', version: '11.5.1'

    /// https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-javafx
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: '12.3.1'

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-core
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-core', version: '12.3.1'

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-material2-pack
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-material2-pack', version: '12.3.1'

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-fontawesome-pack
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-fontawesome-pack', version: '12.3.1'

    // https://mvnrepository.com/artifact/org.kordamp.ikonli/ikonli-materialdesign-pack
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign-pack', version: '12.3.1'


    // -------------------- Testing ------------------------------------
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'

    // https://mvnrepository.com/artifact/org.testfx/testfx-junit5
    testImplementation group: 'org.testfx', name: 'testfx-junit5', version: '4.0.16-alpha'

    // https://mvnrepository.com/artifact/org.testfx/openjfx-monocle
    testImplementation group: 'org.testfx', name: 'openjfx-monocle', version: 'jdk-12.0.1+2'

    // https://mvnrepository.com/artifact/org.mickito/mockito-junit-jupiter
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.2.0'


    //----------------------Dagger (Dependency Injection) --------------
    implementation 'com.google.dagger:dagger:2.45'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.45'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.45'


    // -------------------- Retrofit -----------------------------------
    //https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.9.0'

    //https://mvnrepository.com/artifact/com.squareup.retrofit2/converter-jackson
    implementation group: 'com.squareup.retrofit2', name: 'converter-jackson', version: '2.9.0'

    //https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.2'

    //https://mvnrepository.com/artifact/io.reactivex.rxjava3/rxjava
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.1.6'

    //https://mvnrepository.com/artifact/com.squareup.retrofit2/adapter-rxjava3
    implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava3', version: '2.9.0'


    // -------------------- Websocket ----------------------------------
    //https://mvnrepository.com/artifact/javax.websocket/javax.websocket-api
    compileOnly group: 'javax.websocket', name: 'javax.websocket-api', version: '1.1'

    //https://mvnrepository.com/artifact/javax.json/javax.json-api
    compileOnly group: 'javax.json', name: 'javax.json-api', version: '1.1.4'

    //https://mvnrepository.com/artifact/org.glassfish/javax.json
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'

    // DO NOT UPDATE VERSION. VERSION 2.x.x will break websocket !!!
    //https://mvnrepository.com/artifact/org.glassfish.tyrus/tyrus-client
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-client', version: '1.20'

    // DO NOT UPDATE VERSION. VERSION 2.x.x will break websocket !!!
    //https://mvnrepository.com/artifact/org.glassfish.tyrus/tyrus-container-grizzly-client
    implementation group: 'org.glassfish.tyrus', name: 'tyrus-container-grizzly-client', version: '1.20'
}

javafx {
    version = '20'
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

jacoco {
    toolVersion = '0.8.9'
}

jacocoTestReport {
    reports.xml.required = true
}

test {
    systemProperty "file.encoding", "utf-8"

    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        showStackTraces = true
    }

    if (hasProperty('headless') || System.getenv('CI')) {
        systemProperties = [
                'java.awt.headless': 'true',
                'testfx.robot'     : 'glass',
                'testfx.headless'  : 'true',
                'glass.platform'   : 'Monocle',
                'monocle.platform' : 'Headless',
                'prism.order'      : 'sw',
                'prism.text'       : 't2k',
        ]
    }
}
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
